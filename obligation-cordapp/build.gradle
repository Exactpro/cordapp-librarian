// . ~/.ssh/bintray/${USER}.gradle
// BINTRAY_USER=${USER}
// BINTRAY_KEY=$(< ~/.ssh/bintray/${USER}.apikey)
// BINTRAY_GPG_PASSPHRASE="abrakadabra foo bar"

buildscript {
    ext {
        corda_release_group = "net.corda"
        corda_release_version = "4.0"
        corda_gradle_plugins_version = "4.0.45"
        kotlin_version = "1.2.71"
        junit_version = "4.12"
        quasar_version = "0.7.10"
        log4j_version = "2.11.2"
        slf4j_version = "1.7.25"
        corda_platform_version = 4
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

allprojects {
    apply from: "${rootProject.projectDir}/repositories.gradle"
    apply plugin: 'java'

    configurations {
        compile {
            // We want to use SLF4J's version of these bindings: jcl-over-slf4j
            // Remove any transitive dependency on Apache's version.
            exclude group: 'commons-logging', module: 'commons-logging'
        }
    }

    jar {
        // CorDapps do not configure a Node's logging.
        exclude '**/log4j2*.xml'
    }
}

def publishProjects = [
        project("obligation-j"),
        project("obligation-k")
]

configure(publishProjects) { subproject ->
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    task sourceJar(type: Jar, dependsOn: subproject.classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: subproject.javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    task install(dependsOn: 'publishToMavenLocal')

    publishing {
        publications {
            create(subproject.name, MavenPublication) {
                from components.java

                groupId "$publish_groupId"
                artifactId subproject.jar.baseName
                artifact tasks.sourceJar
                artifact tasks.javadocJar

                pom {
                  description = subproject.description
                  url = 'https://github.com/corda/samples/tree/release-V4/obligation-cordapp'
                  scm {
                    connection = 'scm:git:git@github.com:Exactpro/cordapp-librarian.git'
                  }
                  licenses {
                    license {
                      name = 'The Apache License, Version 2.0'
                      url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                  }
                }
            }
        }

        bintray {
            user = System.getenv('BINTRAY_USER')
            key = System.getenv('BINTRAY_KEY')
            publications = [subproject.name]
            dryRun = false
            publish = true // [Default: false]
            pkg {
                repo = 'cordapp-library'
                name = subproject.name
                userOrg = 'exactpro'
                licenses = ['Apache-2.0']
                vcsUrl = 'https://github.com/Exactpro/cordapp-librarian/tree/librarian/obligation-cordapp'

                version {
                    gpg {
                        sign = true
                        passphrase = System.getenv('BINTRAY_GPG_PASSPHRASE')
                    }
                }
            }
        }
    }
}
